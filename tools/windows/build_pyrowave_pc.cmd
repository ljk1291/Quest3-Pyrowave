@echo off
rem Builds PyroWave for the PC from the workspace clone (research\pyrowave), with the flags the
rem PC has always used:
rem   build-interop : pyrowave-shared.dll/.lib (-DPYROWAVE_DEVEL=OFF), linked by the ALVR server
rem   build-pc      : pyrowave-encode.exe / pyrowave-decode.exe (-DPYROWAVE_DEVEL=ON), offline RD
rem   build-tools   : slangmosh.exe (Granite tools), only needed to regenerate shaders\slangmosh.hpp
rem   rdo-test      : CPU-only encoder-density and decoder-offset parity tests
rem Usage: build_pyrowave_pc.cmd [interop] [pc] [rdo-test] [tools]   (no arguments = interop pc rdo-test)
setlocal
set "WS=%~dp0..\..\.."
if not "%XRWIRED_INPUTS%"=="" set "WS=%XRWIRED_INPUTS%"
set "PW=%WS%\research\pyrowave"
if not exist "%PW%\pyrowave.h" ( echo no pyrowave.h under %PW% & exit /b 1 )
set "GEN=Visual Studio 17 2022"
if not "%Q3PW_CMAKE_GENERATOR%"=="" set "GEN=%Q3PW_CMAKE_GENERATOR%"
set "TARGETS=%*"
if "%TARGETS%"=="" set "TARGETS=interop pc rdo-test"
cd /d "%PW%"
for %%T in (%TARGETS%) do call :%%T || exit /b 1
echo BUILD_OK
exit /b 0

:interop
python "%~dp0..\ci\check_shader_manifest.py" "%PW%" || exit /b 1
cmake -S . -B build-interop -G "%GEN%" -DCMAKE_BUILD_TYPE=Release -DPYROWAVE_DEVEL=OFF -DSHADERC_ENABLE_SHARED_CRT=ON || exit /b 1
cmake --build build-interop --config Release --target pyrowave-shared -j 16 || exit /b 1
dir /b build-interop\Release\*pyrowave-shared*
exit /b 0

:pc
cmake -S . -B build-pc -G "%GEN%" -DCMAKE_BUILD_TYPE=Release -DPYROWAVE_DEVEL=ON -DSHADERC_ENABLE_SHARED_CRT=ON || exit /b 1
cmake --build build-pc --config Release --target pyrowave-encode pyrowave-decode -j 16 || exit /b 1
dir /b build-pc\Release\pyrowave-encode.exe build-pc\Release\pyrowave-decode.exe
exit /b 0

:rdo-test
if not exist build-pc\pyrowave-rdo-density-test.vcxproj ( echo build-pc is not configured; run pc first & exit /b 1 )
if not exist build-pc\pyrowave-dequant-reconstruction-offset-test.vcxproj ( echo decoder offset target is missing; regenerate the source patch & exit /b 1 )
cmake --build build-pc --config Release --target pyrowave-rdo-density-test pyrowave-dequant-reconstruction-offset-test -j 16 || exit /b 1
build-pc\Release\pyrowave-rdo-density-test.exe || exit /b 1
build-pc\Release\pyrowave-dequant-reconstruction-offset-test.exe || exit /b 1
exit /b 0

:tools
cmake -S Granite -B build-tools -G "%GEN%" -DCMAKE_BUILD_TYPE=Release -DGRANITE_TOOLS=ON -DGRANITE_RENDERER=OFF -DGRANITE_VULKAN_SPIRV_CROSS=ON -DGRANITE_VULKAN_SHADER_MANAGER_RUNTIME_COMPILER=ON -DGRANITE_VULKAN_SYSTEM_HANDLES=OFF -DSHADERC_ENABLE_SHARED_CRT=ON || exit /b 1
cmake --build build-tools --config Release --target slangmosh -j 16 || exit /b 1
dir /s /b build-tools\slangmosh.exe
exit /b 0
